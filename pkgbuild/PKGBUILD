# Maintainer: Enlightenment Developers <enlightenment-devel@enlightenment.org>
pkgname=elementary-git
_pkgname=${pkgname%-*}
true && pkgname=('elementary-git' 'elementary_test-git' 'elementary_doc-git')
pkgver=1.8.99.8967.a1cfce6
pkgrel=1
pkgdesc="Enlightenment GUI toolkit - GIT development snapshot"
arch=('i686' 'x86_64' 'arm')
url="http://www.enlightenment.org"
license=('LGPL2.1' 'CCPL:cc-by-sa')
makedepends=('doxygen' 'imagemagick' 'git')
depends=('efl-git')
  [[ ! $(pacman -T ewebkit-svn) ]] && depends+=('ewebkit-svn')         # webkit-efl support is detected at build time
  [[ ! $(pacman -T elocation-git) ]] && depends+=('elocation-git')     # elocation support is detected at build time
  [[ ! $(pacman -T libeweather-git) ]] && depends+=('libeweather-git') # eweather support is detected at build time
optdepends=('emotion_generic_players' 'evas_generic_loaders')
options=('!libtool' 'debug')
provides=('elementary')

pkgver() {
  cd "../.."

  for _i in v_maj v_min v_mic; do
    local v_ver=$v_ver.$(grep -m 1 $_i configure.ac | sed 's/m4//' | grep -o "[[:digit:]]*")
  done

  v_ver=$(awk -F , -v v_ver=${v_ver#.} '/^AC_INIT/ {gsub(/v_ver/, v_ver); gsub(/[\[\] -]/, ""); print $2}' configure.ac)

  printf "$v_ver.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "../.."
  export CFLAGS="$CFLAGS -fvisibility=hidden -fomit-frame-pointer"
  [[ -e Makefile ]] && make clean distclean
  ./configure --prefix=/usr \
  --disable-static
  make
  make doc
}

#check() {
#  cd "../.."
#  make -k check
#}

package_elementary-git() {
  cd "../.."
  make -j1 DESTDIR="$pkgdir/" install
  install -Dm644 README "$pkgdir/usr/share/$pkgname/README"
  install -Dm644 NEWS "$pkgdir/usr/share/$pkgname/NEWS"
  install -Dm644 ChangeLog "$pkgdir/usr/share/$pkgname/ChangeLog"
  install -Dm644 AUTHORS "$pkgdir/usr/share/$pkgname/AUTHORS"
  install -Dm644 COMPLIANCE "$pkgdir/usr/share/$pkgname/COMPLIANCE"
  install -Dm644 COPYING.images "$pkgdir/usr/share/licenses/$pkgname/COPYING.images"
  sed -n '1,/details./p' COPYING > "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  ln -s "../licenses/$pkgname/COPYING" "$pkgdir/usr/share/$pkgname/COPYING"
  ln -s "../licenses/$pkgname/COPYING.images" "$pkgdir/usr/share/$pkgname/COPYING.images"
  rm -rf "$pkgdir/usr/bin/"elementary_test
  rm -rf "$pkgdir/usr/lib/elementary/modules/"{test_entry,test_map}
  rm -rf "$pkgdir/usr/share/applications/elementary_test.desktop"
  rm -rf "$pkgdir/usr/share/elementary/"{examples,images,objects}
}

package_elementary_test-git() {
  pkgdesc="Test application for Elementary"
  depends=('elementary')

  cd "../.."
  make -j1 DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/usr/include"
  rm -rf "$pkgdir/usr/bin/"{elementary_codegen,elementary_config,elementary_quicklaunch,elementary_run,elm_prefs_cc}
  rm -rf "$pkgdir/usr/lib/"{cmake,edje,libelementary.so*,pkgconfig}
  rm -rf "$pkgdir/usr/lib/elementary/modules/"{access_output,datetime_input_ctxpopup,prefs}
  rm -rf "$pkgdir/usr/share/applications/elementary_config.desktop"
  rm -rf "$pkgdir/usr/share/elementary/"{config,examples,themes,edje_externals}
  rm -rf "$pkgdir/usr/share/"{icons,locale}
}

package_elementary_doc-git() {
  pkgdesc="Documentation for Elementary"
  arch=('any')
  unset depends optdepends

  cd "../.."
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp -a doc/html "$pkgdir/usr/share/doc/$pkgname/html"
  cp -a doc/latex "$pkgdir/usr/share/doc/$pkgname/latex"
}
